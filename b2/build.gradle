apply plugin: 'war'

sourceCompatibility = 11
targetCompatibility = 11

version = rootProject.version

ext {
    deployServer = "http://localhost:9876"
    deployInstance = "BBLEARN"
}

configurations {
    deployB2
}

repositories {
    mavenCentral()
    maven {
        url "https://bbprepo.blackboard.com/content/repositories/releases/"
    }
}

dependencies {
    providedCompile 'blackboard.platform:bb-platform:3700.99.0',
                    'blackboard.platform:bb-taglibs:3700.99.0',
                    'javax.servlet:javax.servlet-api:3.0.1',
                    'jstl:jstl:1.2',
                    'com.google.guava:guava:16.0'

    compile 'org.slf4j:slf4j-api:1.7.28'
    compile 'org.slf4j:jul-to-slf4j:1.7.28'
    compile 'ch.qos.logback:logback-classic:1.2.3'
    compile 'org.slf4j:log4j-over-slf4j:1.7.28'

    compile 'javax.inject:javax.inject:1',
            'org.springframework:spring-orm:4.3.0.RELEASE',
            'org.springframework:spring-beans:4.3.0.RELEASE',
            'org.springframework:spring-context:4.3.0.RELEASE',
            'org.springframework:spring-web:4.3.0.RELEASE',
            'blackboard.platform:bb-spring-webapi:10.1',
            'com.google.code.gson:gson:2.8.6'

    compile ('org.hibernate:hibernate-core:4.3.11.Final') {
        exclude group: 'xml-apis', module: 'xml-apis'
    }
    compile ('org.hibernate:hibernate-entitymanager:4.3.11.Final') {
        exclude group: 'xml-apis', module: 'xml-apis'
    }

    compile 'net.sourceforge.stripes:stripes:1.6.0',
            files('../lib/bb-stripes-utils-1.1.3.jar'),
            files('../lib/b2-logging-utils-1.0.2.jar')

    compile project(':api')

    //Ant deploy
    deployB2 "org.oscelot:b2deploy-task:0.1.0"

    testCompile 'junit:junit:4.12',
                'org.mockito:mockito-core:2.28.2'
}

war {
    // Changes the produced WAR files name to the name of the root project.
    baseName rootProject.name
}

build.dependsOn(project(':api').build);

task deployB2( dependsOn: "build" ) {
    doLast {
        ant.taskdef(name: "b2deploy", classname: "org.oscelot.ant.B2DeployTask", classpath: project.configurations.deployB2.asPath)
        ant.b2deploy(localfilepath: project.war.archivePath, host: project.ext.deployServer, courseorgavailable: 'true', clean: 'true', webappName: 'bb-starting-block-' + project.ext.deployInstance)
    }
}
